PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX csvw: <http://www.w3.org/ns/csvw#>

SELECT DISTINCT 
    ?componentProperty ?componentPropertyLabel ?componentOrder ?componentPropertyType ?csvColumnTitle ?observationValueColumnTitles
    (COALESCE(?csvColumnRequired, false) || ?componentRequired as ?required) 
WHERE {   
    { 
        SELECT DISTINCT ?csvColumnTitle ?observationValueColumnTitles ?csvColumnRequired ?component ?componentProperty ?componentOrder 
        WHERE {
            {
                SELECT ?csvColumnPropertyUrl ?columnTitle (GROUP_CONCAT(?observationValueColumnTitle; separator=', ') as ?observationValueColumnTitles)
                WHERE 
                {
                    ?dsd_uri a qb:DataStructureDefinition;
                             # Get list of possible measures in this DSD
                             qb:component/qb:measure ?measureUri.

                    ?tableSchema csvw:column/rdf:rest*/rdf:first ?csvColumn.

                    OPTIONAL {
                        ?csvColumn csvw:title ?columnTitle.
                    }
                                 
                    # BIND("Hello" as ?observationValueColumnTitle).

                    OPTIONAL {
                        ?csvColumn csvw:aboutUrl ?csvColumnAboutUrl;
                                   csvw:propertyUrl ?csvColumnPropertyUrl.


                        ?tableSchema csvw:column/rdf:rest*/rdf:first ?observedValueColumn.
                        FILTER NOT EXISTS {
                            ?observedValueColumn csvw:virtual true.
                        }

                        # The obs val column for this column is the one matching on the aboutUrl with a 
                        # valid measureUri in the propertyUrl field.
                        ?observedValueColumn csvw:aboutUrl ?csvColumnAboutUrl;
                                             csvw:propertyUrl ?observedValueColPropertyUrl;
                                             csvw:title ?observationValueColumnTitle.

                        FILTER (strends(str(?observedValueColPropertyUrl), str(?measureUri)) || strends(str(?measureUri), str(?observedValueColPropertyUrl))).
                    }
                }
                GROUP BY ?csvColumnPropertyUrl ?columnTitle
            }

            ?dsd_uri a qb:DataStructureDefinition.

            ?dsd_uri qb:component ?component.

            ?component qb:componentProperty|qb:dimension|qb:measureDimension|qb:measure|qb:attribute ?componentProperty;
                    qb:order ?componentOrder.

            OPTIONAL {
                ?csvColumn csvw:propertyUrl ?csvColumnPropertyUrl.

                OPTIONAL { ?csvColumn csvw:title ?csvColumnTitle. }

                {
                    ?csvColumn csvw:required ?csvColumnRequired.
                } UNION {
                    BIND(false as ?csvColumnRequired).
                    FILTER NOT EXISTS {
                        ?csvColumn csvw:required ?csvColumnRequired.
                    }
                }

                FILTER(STRENDS(str(?componentProperty), str(?csvColumnPropertyUrl))).
            } 
        }
    }

    OPTIONAL {
        ?componentProperty rdfs:label ?componentPropertyLabel.
    }

    OPTIONAL {
        # https://www.w3.org/TR/vocab-data-cube/#h3_reference-compspec
        {
            ?component qb:dimension ?dimension.
            BIND (qb:DimensionProperty as ?componentPropertyType).
            BIND (true as ?componentRequired)
        } UNION {
            # https://www.w3.org/TR/vocab-data-cube/#dfn-qb-measuredimension
            ?component qb:measureDimension ?dimension.
            BIND (qb:MeasureProperty as ?componentPropertyType).
            BIND (true as ?componentRequired)
        } UNION {
            ?component qb:measure ?measure.
            BIND (qb:MeasureProperty as ?componentPropertyType).
            BIND (true as ?componentRequired)
        } UNION {
            ?component qb:attribute ?attribute.
            BIND (qb:AttributeProperty as ?componentPropertyType).

            {
                ?component qb:componentRequired ?componentRequired.
            } UNION {
                BIND (false as ?componentRequired)
                FILTER NOT EXISTS {
                    ?component qb:componentRequired [].
                }
            }
        } 
    }
}    
ORDER BY ASC(?componentOrder)