PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX csvw: <http://www.w3.org/ns/csvw#>

SELECT ?dsdUri ?component ?componentProperty ?componentPropertyLabel ?componentOrder ?componentPropertyType ?required
WHERE {

    {
        SELECT DISTINCT ?dsdUri ?component ?componentProperty
        WHERE {
            ?dsdUri qb:component ?component.

            ?component qb:componentProperty|qb:dimension|qb:measureDimension|qb:measure|qb:attribute ?componentProperty.
        }
    }

    ?component qb:order ?componentOrder

    OPTIONAL { ?componentProperty rdfs:label ?componentPropertyLabel. }

    # https://www.w3.org/TR/vocab-data-cube/#h3_reference-compspec
    {
        ?component qb:dimension ?dimension.
        BIND (qb:DimensionProperty as ?componentPropertyType).
        BIND (true as ?required)
    } UNION {
        # https://www.w3.org/TR/vocab-data-cube/#dfn-qb-measuredimension
        ?component qb:measureDimension ?dimension.
        BIND (qb:MeasureProperty as ?componentPropertyType).
        BIND (true as ?required)
    } UNION {
        ?component qb:measure ?measure.
        BIND (qb:MeasureProperty as ?componentPropertyType).
        BIND (true as ?required)
    } UNION {
        ?component qb:attribute ?attribute.
        BIND (qb:AttributeProperty as ?componentPropertyType).

        {
            ?component qb:componentRequired ?componentRequired.
        } UNION {
            BIND (false as ?required)
            FILTER NOT EXISTS {
                ?component qb:componentRequired [].
            }
        }
    }
}    
ORDER BY ASC(?componentOrder)